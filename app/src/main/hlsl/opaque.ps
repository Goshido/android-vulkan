[[vk::binding ( 0, 0 )]]
Texture2D<float32_t4>               g_diffuseTexture:       register ( t0 );

[[vk::binding ( 1, 0 )]]
SamplerState                        g_diffuseSampler:       register ( s0 );

[[vk::binding ( 2, 0 )]]
Texture2D<float32_t4>               g_emissionTexture:      register ( t1 );

[[vk::binding ( 3, 0 )]]
SamplerState                        g_emissionSampler:      register ( s1 );

[[vk::binding ( 4, 0 )]]
Texture2D<float32_t4>               g_maskTexture:          register ( t2 );

[[vk::binding ( 5, 0 )]]
SamplerState                        g_maskSampler:          register ( s2 );

[[vk::binding ( 6, 0 )]]
Texture2D<float32_t4>               g_normalTexture:        register ( t3 );

[[vk::binding ( 7, 0 )]]
SamplerState                        g_normalSampler:        register ( s3 );

[[vk::binding ( 8, 0 )]]
Texture2D<float32_t4>               g_paramTexture:         register ( t4 );

[[vk::binding ( 9, 0 )]]
SamplerState                        g_paramSampler:         register ( s4 );

struct InputData
{
    [[vk::location ( 0 )]]
    linear float32_t2               _uv:                    UV;

    [[vk::location ( 1 )]]
    linear float32_t3               _normalView:            NORMAL;

    [[vk::location ( 2 )]]
    linear float32_t3               _tangentView:           TANGENT;

    [[vk::location ( 3 )]]
    linear float32_t3               _bitangentView:         BITANGENT;

    [[vk::location ( 4 )]]
    nointerpolation float32_t4      _color0:                COLOR_0;

    [[vk::location ( 5 )]]
    nointerpolation float32_t4      _color1:                COLOR_1;

    [[vk::location ( 6 )]]
    nointerpolation float32_t4      _color2:                COLOR_2;

    [[vk::location ( 7 )]]
    nointerpolation float32_t4      _color3:                COLOR_3;
};

struct OutputData
{
    float32_t4                      _albedo:                SV_Target0;
    float32_t4                      _emission:              SV_Target1;
    float32_t4                      _normal:                SV_Target2;
    float32_t4                      _param:                 SV_Target3;
};

//----------------------------------------------------------------------------------------------------------------------

float32_t3 GetFinalNormalView ( in float32_t3 tangentView,
    in float32_t3 bitangentView,
    in float32_t3 normalView,
    in float32_t2 uv
)
{
    const float32_t2 normalData = mad ( g_normalTexture.Sample ( g_normalSampler, uv ).xw,
        (float32_t2)2.0H,
        (float32_t2)( -1.0H )
    );

    const float32_t3x3 tbnView = float32_t3x3 ( tangentView, bitangentView, normalView );

    // DISCLAMER: You might guess it's a good idea for optimization to not perform normalize operation here.
    // In reality it's bad idea. In fact you will see noticable artifacts with specular on the light-up pass.
    // And you will NOT see any performance benefits in terms of FPS on real game scenes.
    // Trust me. I tried (2021-01-27)...
    return normalize ( mul ( float32_t3 ( normalData.xy, 1.0F ), tbnView ) );
}

float32_t4 PackUnitVector ( in float32_t3 v )
{
    return float32_t4 ( mad ( v, 0.5F, 0.5F ), 0.5F );
}

OutputData PS ( in InputData inputData )
{
    float32_t3 maskSample = g_maskTexture.Sample ( g_maskSampler, inputData._uv ).xyz;
    const float32_t maskSum = maskSample.x + maskSample.y + maskSample.z;

    if ( maskSum > 1.0F )
        maskSample *= 1.0F / maskSum;

    float32_t3 albedoOverlay = (float32_t3)max ( 1.0F - maskSum, 0.0F );
    albedoOverlay = mad ( inputData._color0.xyz, maskSample.x, albedoOverlay );
    albedoOverlay = mad ( inputData._color1.xyz, maskSample.y, albedoOverlay );
    albedoOverlay = mad ( inputData._color2.xyz, maskSample.z, albedoOverlay );

    OutputData result;

    result._albedo =
        float32_t4 ( albedoOverlay * g_diffuseTexture.Sample ( g_diffuseSampler, inputData._uv ).xyz, 1.0F );

    result._emission = g_emissionTexture.Sample ( g_emissionSampler, inputData._uv );
    result._param = g_paramTexture.Sample ( g_paramSampler, inputData._uv );

    result._normal = PackUnitVector (
        GetFinalNormalView ( inputData._tangentView, inputData._bitangentView, inputData._normalView, inputData._uv )
    );

    return result;
}
