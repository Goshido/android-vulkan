#include "lightup-common.ps"

// The implementation is based on ideas from LearnOpenGL resourse
// see https://learnopengl.com/PBR/Theory
// see https://learnopengl.com/PBR/Lighting

#define EPSILON             1.0e-3H

#define PI                  3.141592H
#define INVERSE_PI          0.31830H

// It was set by empirical test.
#define SHADOW_MAP_BIAS     9.9e-1F

[[vk::binding ( 0, 1 )]]
TextureCube<float>          g_shadowmap:    register ( t0 );

[[vk::binding ( 1, 1 )]]
SamplerComparisonState      g_sampler:      register ( s0 );

[[vk::binding ( 2, 1 )]]
cbuffer LightData:                          register ( b1 )
{
    matrix                  _lightProjection;
    matrix                  _viewToLight;

    float3                  _lightLocationView;

    // Must be equal: 1.0F / units-per-meter
    float                   _sceneScaleFactor;

    half3                   _hue;
    half                    _intensity;

    float2                  _padding1_0;
};

struct OutputData
{
    [[vk::location (0)]]
    float4                  _color:         SV_Target0;
};

//----------------------------------------------------------------------------------------------------------------------

half3 GetDiffuseK ( in half3 fesnelFactor, in half metallic )
{
    return ( 1.0H - fesnelFactor ) * ( 1.0H - metallic );
}

half GetDirectK ( in half roughness )
{
    const half alpha = roughness + 1.0H;
    return alpha * alpha * 0.125H;
}

half GGXSchlick ( in half dotFactor, in half k )
{
    return dotFactor / mad ( dotFactor, 1.0H - k, k );
}

half GetAttenuation ( in float3 fragToLight )
{
    const half beta = (half)max ( 1.0F, _sceneScaleFactor * length ( fragToLight ) );
    return _intensity / ( beta * beta );
}

half GetDistribution ( in half nH, in half roughness )
{
    const half alpha = roughness * roughness;
    const half beta = alpha * alpha;
    const half gamma = nH * nH;
    const half zeta = mad ( gamma, beta - 1.0H, 1.0H );

    return beta / ( PI * zeta * zeta );
}

half GetGeometryAttenuation ( in half nL, in half nV, in half roughness )
{
    const half k = GetDirectK ( roughness );
    return GGXSchlick ( nL, k ) * GGXSchlick ( nV, k );
}

half GetShadowFactor ( in float3 locationView )
{
    const float3 dir = mul ( _viewToLight, float4 ( locationView, 1.0F ) ).xyz * SHADOW_MAP_BIAS;
    const float3 faceSelector = abs ( dir );
    const float z = max ( faceSelector.x, max ( faceSelector.y, faceSelector.z ) );
    const float4 cvv = mul ( _lightProjection, float4 ( 0.0F, 0.0F, z, 1.0F ) );

    return (half)g_shadowmap.SampleCmpLevelZero ( g_sampler, dir, cvv.z / cvv.w );
}

float3 RestoreLocationView ( in float2 locationImage )
{
    const float2 alpha = mad ( locationImage, _invResolutionFactor, (float2)( -1.0F ) );
    const float4 v = mul ( _cvvToView, float4 ( alpha, GetDepth (), 1.0F ) );
    return v.xyz * ( 1.0F / v.w );
}

//----------------------------------------------------------------------------------------------------------------------

OutputData PS ( in linear float4 pixel: SV_Position )
{
    const float3 locationView = RestoreLocationView ( pixel.xy );
    const float3 fragToLight = _lightLocationView - locationView;

    const half attenuation = GetAttenuation ( fragToLight );
    const half3 toViewerView = -normalize ( (half3)locationView );
    const half3 toLightView = normalize ( (half3)fragToLight );
    const half3 halfVectorView = normalize ( toLightView + toViewerView );
    const half3 normalView = RestoreNormalView ();

    half roughness;
    half metallic;
    GetParams ( roughness, metallic );

    const half3 albedo = GetAlbedo ();

    const half nL = max ( 0.0H, dot ( normalView, toLightView ) );
    const half nH = max ( 0.0H, dot ( normalView, halfVectorView ) );
    const half nV = max ( 0.0H, dot ( normalView, toViewerView ) );

    const half d = GetDistribution ( nH, roughness );
    const half3 f = GetFresnel ( nH, metallic, albedo );
    const half ga = GetGeometryAttenuation ( nL, nV, roughness );
    const half shadow = GetShadowFactor ( locationView );
    const half3 kD = GetDiffuseK ( f, metallic );

    const half3 specular = f * d * ga / ( max ( EPSILON, 4.0H * nL * nV ) );
    const half3 color = shadow * nL * attenuation * _hue * mad ( kD * albedo, INVERSE_PI, specular );

    OutputData result;
    result._color = float4 ( (float3)color, 1.0F );

    return result;
}
