import org.jetbrains.kotlin.gradle.dsl.jvm.JvmTargetValidationMode
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

plugins {
    id "com.android.application"
    id "kotlin-android"
}

android {
    // It's deprecated now. But I have no clue how to specify explicit build tool version.
    // noinspection GrDeprecatedAPIUsage
    buildToolsVersion "36.0.0"

    ndkVersion "28.1.13356709"
    namespace 'com.goshidoInc.androidVulkan'

    defaultConfig {
        applicationId "com.goshidoInc.androidVulkan"
        compileSdk 36
        minSdkVersion 30
        versionCode 1
        versionName "1.0"
    }

    // Release stuff and certificates. No hardcode. All settings are delegated to the user environment.
    // Based on https://gist.github.com/lifuzu/9822276
    signingConfigs {
        release {
            storeFile file(System.getenv('ANDROID_VULKAN_CERTIFICATE_STORE_FILE'))
            storePassword System.getenv('ANDROID_VULKAN_CERTIFICATE_STORE_PASSWORD')
            keyAlias System.getenv('ANDROID_VULKAN_CERTIFICATE_KEY_ALIAS')
            keyPassword System.getenv('ANDROID_VULKAN_CERTIFICATE_KEY_PASSWORD')
        }
    }

    buildTypes {
        debug {
            ndk {
                // noinspection ChromeOsAbiSupport
                abiFilters "arm64-v8a"
            }
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            ndk {
                // noinspection ChromeOsAbiSupport
                abiFilters "arm64-v8a"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path file('../CMakeLists.txt')
            version "4.0.2"
        }
    }

    sourceSets {
        main {
            ext.makePrebuildLibraryPath = { _ ->
                return project.rootDir.toString() + "/third-party/jniLibs"
            }

            jniLibs {
                srcDir makePrebuildLibraryPath(null)
            }

            java.srcDirs += "src/main/kotlin"
        }
    }
}

// [2024-11-23] This allows to use recent Kotlin toolchain. Without it you will get the following error:
// Inconsistent JVM-target compatibility detected for tasks 'compileDebugJavaWithJavac' (1.8) and
// 'compileDebugKotlin' (21).
// See https://kotlinlang.org/docs/gradle-configure-project.html#-5yydp1_177
tasks.withType(KotlinJvmCompile.class).configureEach {
    jvmTargetValidationMode = JvmTargetValidationMode.IGNORE
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
