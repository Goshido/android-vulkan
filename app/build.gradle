plugins {
    id "com.android.application"
    id "kotlin-android"
}

android {
    compileSdkVersion 31
    buildToolsVersion "33.0.0"
    ndkVersion "25.0.8775105"
    namespace 'com.goshidoInc.androidVulkan'

    defaultConfig {
        applicationId "com.goshidoInc.androidVulkan"
        minSdkVersion 30
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
    }

    // Release stuff and certificates. No hardcode. All delegates to the user environment.
    // Based on https://gist.github.com/lifuzu/9822276
    signingConfigs {
        release {
            storeFile file(System.getenv('ANDROID_VULKAN_CERTIFICATE_STORE_FILE'))
            storePassword System.getenv('ANDROID_VULKAN_CERTIFICATE_STORE_PASSWORD')
            keyAlias System.getenv('ANDROID_VULKAN_CERTIFICATE_KEY_ALIAS')
            keyPassword System.getenv('ANDROID_VULKAN_CERTIFICATE_KEY_PASSWORD')
        }
    }

    buildTypes {
        debug {
            ndk {
                abiFilters "arm64-v8a"
            }
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            ndk {
                abiFilters "arm64-v8a"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path file('../CMakeLists.txt')
            version "3.22.1"
        }
    }

    sourceSets {
        main {
            ext.makePrebuildLibraryPath = { ->
                return project.rootDir.toString() + "/third-party/jniLibs"
            }

            jniLibs {
                srcDir makePrebuildLibraryPath()

            }

            java.srcDirs += "src/main/kotlin"
        }
    }

    // Solving '2 files found with path' error [CMake 3.22.1]. Based on ideas from
    // https://dtuto.com/questions/8657/2-files-found-with-path-lib-arm64-v8a-libc-shared-so-from-inputs-react-native
    packagingOptions {
        jniLibs.pickFirsts.add("lib/arm64-v8a/libLua.so")
        jniLibs.pickFirsts.add("lib/arm64-v8a/libVkLayer_khronos_validation.so")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
