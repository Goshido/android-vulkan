plugins {
    id "com.android.application"
    id "kotlin-android"
}

android {
    // It's deprecated now. But I have no clue how to specify explicit build tool version.
    // noinspection GrDeprecatedAPIUsage
    buildToolsVersion "34.0.0"

    ndkVersion "26.1.10909125"
    namespace 'com.goshidoInc.androidVulkan'

    defaultConfig {
        applicationId "com.goshidoInc.androidVulkan"
        compileSdk 34
        minSdkVersion 30
        versionCode 1
        versionName "1.0"
    }

    // [2023-04-25] This allows to use recent Kotlin toolchain. Without it you will get the following error:
    // 'compileDebugJavaWithJavac' task (current target is 1.8) and 'compileDebugKotlin' task
    // (current target is 17) jvm target compatibility should be set to the same Java version.
    // Consider using JVM toolchain: https://kotl.in/gradle/jvm/toolchain
    //
    // See https://kotlinlang.org/docs/gradle-configure-project.html#gradle-java-toolchains-support
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // Release stuff and certificates. No hardcode. All settings are delegated to the user environment.
    // Based on https://gist.github.com/lifuzu/9822276
    signingConfigs {
        release {
            storeFile file(System.getenv('ANDROID_VULKAN_CERTIFICATE_STORE_FILE'))
            storePassword System.getenv('ANDROID_VULKAN_CERTIFICATE_STORE_PASSWORD')
            keyAlias System.getenv('ANDROID_VULKAN_CERTIFICATE_KEY_ALIAS')
            keyPassword System.getenv('ANDROID_VULKAN_CERTIFICATE_KEY_PASSWORD')
        }
    }

    buildTypes {
        debug {
            ndk {
                // noinspection ChromeOsAbiSupport
                abiFilters "arm64-v8a"
            }
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            ndk {
                // noinspection ChromeOsAbiSupport
                abiFilters "arm64-v8a"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path file('../CMakeLists.txt')
            version "3.22.1"
        }
    }

    sourceSets {
        main {
            ext.makePrebuildLibraryPath = { _ ->
                return project.rootDir.toString() + "/third-party/jniLibs"
            }

            jniLibs {
                srcDir makePrebuildLibraryPath(null)

            }

            java.srcDirs += "src/main/kotlin"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
